Процедура ОбработкаПроведения(Отказ, Режим)

	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	РасчетРецептаЗатратыЗолото.Номенклатура,
	|	РасчетРецептаЗатратыЗолото.Цена
	|ПОМЕСТИТЬ втСбор
	|ИЗ
	|	Документ.РасчетРецепта.ЗатратыЗолото КАК РасчетРецептаЗатратыЗолото
	|ГДЕ
	|	РасчетРецептаЗатратыЗолото.Ссылка = &Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	РасчетРецептаПолучили.Номенклатура,
	|	РасчетРецептаПолучили.Цена
	|ИЗ
	|	Документ.РасчетРецепта.Получили КАК РасчетРецептаПолучили
	|ГДЕ
	|	РасчетРецептаПолучили.Ссылка = &Ссылка
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втСбор.Номенклатура,
	|	МАКСИМУМ(втСбор.Цена) КАК Цена
	|ИЗ
	|	втСбор КАК втСбор
	|СГРУППИРОВАТЬ ПО
	|	втСбор.Номенклатура";

	Запрос.Параметры.Вставить("Ссылка", Ссылка);
	РезультатЗапроса = Запрос.Выполнить();

	Выборка = РезультатЗапроса.Выбрать();

	Движения.ЦеныНоменклатуры.Записывать = Истина;
	Пока Выборка.Следующий() Цикл
		Движение 				= Движения.ЦеныНоменклатуры.Добавить();
		Движение.Период			= Дата;
		Движение.Номенклатура 	= Выборка.Номенклатура;
		Движение.Цена 			= Выборка.Цена;
	КонецЦикла;

	Движения.МаржинальностьРецепта.Записывать = Истина;
	Движение = Движения.МаржинальностьРецепта.Добавить();
	Движение.Период			= Дата;
	Движение.Рецепт 		= Рецепт;
	Движение.ОР1000 		= ОР1000;
	Движение.ОР1000РемРеп 	= ОР1000РемРеп;
КонецПроцедуры